# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-gtk3
pkgver=0.33
pkgrel=7
pkgdesc="Gtk client and libraries for SPICE remote desktop servers (gtk3 version)"
arch=('i686' 'x86_64')
url="https://www.spice-space.org"
license=('LGPL2.1')
depends=('celt0.5.1' 'dbus-glib' 'gtk3' 'libcacard' 'libpulse' 'usbredir' 'libsoup' 'spice-glib')
makedepends=('gobject-introspection' 'intltool' 'python2-pyparsing' 'spice-protocol' 'usbutils'
	     'vala' 'sdl' 'python2-six')
options=('!emptydirs')
source=("https://www.spice-space.org/download/gtk/spice-gtk-$pkgver.tar.bz2"
        debian_843471-clipboard_Fix_crash_by_handling_error.patch
        ssl-Stop-creating-our-own-X509_LOOKUP_METHOD.patch
        ssl-Rework-our-custom-BIO-type.patch
        ssl-Use-accessors-rather-than-direct-struct-access.patch)
sha256sums=('7daf33b786ab8ebc9fe6e7b11745100ee1ab66e61a5811d1d5aabe4d66eaaf39'
            '31cacbfce3de9c7599f3cca5b45ed76cf5e3997072aae64b4107676b0b9a2f7b'
            '0e0d553992ea08a29b14439061c6e831ce4ba4bd7bb5e53d7e7e1306aa6ba0fa'
            '91bbb28dc5a8968c0eea57919d9a4a86384a62909014b53b05b5e721afa8142d'
            '0e65852f94849c3efe0c21c01f4be7b804641d7d178b12a22eb62f08db70371e')

prepare() {
  cd "$srcdir/spice-gtk-$pkgver"
  patch -Np1 -i ../debian_843471-clipboard_Fix_crash_by_handling_error.patch
  patch -Np1 -i ../ssl-Stop-creating-our-own-X509_LOOKUP_METHOD.patch
  patch -Np1 -i ../ssl-Rework-our-custom-BIO-type.patch
  patch -Np1 -i ../ssl-Use-accessors-rather-than-direct-struct-access.patch
}

build() {
  cd "$srcdir/spice-gtk-$pkgver"
  PYTHON=python2 ./configure --prefix=/usr \
    --disable-static --enable-vala --with-gtk=3.0 \
    --with-audio=pulse \
    --with-coroutine=gthread --sbindir=/usr/bin \
    --enable-smartcard
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "$srcdir/spice-gtk-$pkgver"
  make DESTDIR="$pkgdir/" install
  pacman -Ql spice-glib | cut -f2 -d' ' | while read f; do
    [ -f "$pkgdir"/$f ] && rm -f "$pkgdir"/$f || true
    f=$(echo $f | sed 's|.gz$||')
    [ -f "$pkgdir"/$f ] && rm -f "$pkgdir"/$f || true
  done
}
